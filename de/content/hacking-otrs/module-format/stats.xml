<?xml version='1.0' encoding='UTF-8'?>
<!DOCTYPE section PUBLIC "-//OASIS//DTD DocBook XML V4.2//EN" "http://www.oasis-open.org/docbook/xml/4.2/docbookx.dtd">

<!-- $Id: stats.xml,v 1.2 2010-12-01 13:31:52 mg Exp $ -->
<section id="module-stats">
    <title>Stats Module</title>
    <para>
There are two different types of internal stats modules - dynamic and
static. This section describes how such stats modules can be developed.
    </para>
    <section id="module-stats-dynamic">
        <title>Dynamic Stats</title>
        <para>
In contrast to static stats modules, dynamic statistics can be configured
via the OTRS web interface. In this section a simple statistic module is
developed. Each dynamic stats module has to implement these subroutines
        </para>
        <para>
            <itemizedlist mark="round">
                <listitem>
                    <para>neu</para>
                </listitem>
                <listitem>
                    <para>GetObjectName</para>
                </listitem>
                <listitem>
                    <para>GetObjectAttributes</para>
                </listitem>
                <listitem>
                    <para>ExportWrapper</para>
                </listitem>
                <listitem>
                    <para>ImportWrapper</para>
                </listitem>
            </itemizedlist>
        </para>
        <para>
Furthermore the module has to implement either GetStatElement or
GetStatTable. And if the header line of the result table should be changed,
a sub called GetHeaderLine has to be developed.
        </para>
        <section id="module-stats-dynamic-subroutines">
            <title>Code example</title>
            <para>
In this section a sample stats module is shown and each subroutine is
explained.
            </para>
            <para>
                <programlisting><![CDATA[
# --
# Kernel/System/Stats/Dynamic/DynamicStatsTemplate.pm - all advice functions
# Copyright (C) 2001-2010 OTRS AG, http://otrs.org/
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (AGPL). If you
# did not receive this file, see http://www.gnu.org/licenses/agpl.txt.
# --

package Kernel::System::Stats::Dynamic::DynamicStatsTemplate;

use strict;
use warnings;

use Kernel::System::Queue;
use Kernel::System::State;
use Kernel::System::Ticket;

use vars qw($VERSION);
$VERSION = qw($Revision: 1.2 $) [1];
                ]]></programlisting>
            </para>
            <para>
This is common boilerplate that can be found in common OTRS modules. The
class/package name is declared via the package keyword. Then the needed
modules are 'use'd.
            </para>
            <para>
                <programlisting><![CDATA[
sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    # check needed objects
    for my $Object (
        qw(DBObject ConfigObject LogObject UserObject TimeObject MainObject EncodeObject)
        )
    {
        $Self->{$Object} = $Param{$Object} || die "Got no $Object!";
    }

    # created needed objects
    $Self->{QueueObject}    = Kernel::System::Queue->new( %{$Self} );
    $Self->{TicketObject}   = Kernel::System::Ticket->new( %{$Self} );
    $Self->{StateObject}    = Kernel::System::State->new( %{$Self} );

    return $Self;
}
                ]]></programlisting>
            </para>
            <para>
new is the constructor for this statistic module. It creates a new instance
of the class. According to the coding guidelines objects of other classes
that are needed in this module have to be created in "new". In lines 27 to
29 the object of the stats module is created. Lines 31 to 37 check if
objects that are needed in this code - either for creating other objects or
in this module - are passed. After that the other objects are created.
            </para>
            <para>
                <programlisting><![CDATA[
sub GetObjectName {
    my ( $Self, %Param ) = @_;

    return 'Sample Statistics';
}
                ]]></programlisting>
            </para>
            <para>
GetObjectName returns a Name for the Statistics module. This is the label
that is shown in the drop down in the configuration as well as in the list
of existing statistics (column "object").
            </para>
            <para>
                <programlisting><![CDATA[
sub GetObjectAttributes {
    my ( $Self, %Param ) = @_;

    # get state list
    my %StateList = $Self->{StateObject}->StateList(
        UserID => 1,
    );

    # get queue list
    my %QueueList = $Self->{QueueObject}->GetAllQueues();

    # get current time to fix bug#3830
    my $TimeStamp = $Self->{TimeObject}->CurrentTimestamp();
    my ($Date) = split /\s+/, $TimeStamp;
    my $Today = sprintf "%s 23:59:59", $Date;

    my @ObjectAttributes = (
        {
            Name             => 'State',
            UseAsXvalue      => 1,
            UseAsValueSeries => 1,
            UseAsRestriction => 1,
            Element          => 'StateIDs',
            Block            => 'MultiSelectField',
            Values           => \%StateList,
        },
        {
            Name             => 'Created in Queue',
            UseAsXvalue      => 1,
            UseAsValueSeries => 1,
            UseAsRestriction => 1,
            Element          => 'CreatedQueueIDs',
            Block            => 'MultiSelectField',
            Translation      => 0,
            Values           => \%QueueList,
        },
        {
            Name             => 'Create Time',
            UseAsXvalue      => 1,
            UseAsValueSeries => 1,
            UseAsRestriction => 1,
            Element          => 'CreateTime',
            TimePeriodFormat => 'DateInputFormat',    # 'DateInputFormatLong',
            Block            => 'Time',
            TimeStop         => $Today,
            Values           => {
                TimeStart => 'TicketCreateTimeNewerDate',
                TimeStop  => 'TicketCreateTimeOlderDate',
            },
        },
    );

    return @ObjectAttributes;
}
                ]]></programlisting>
            </para>
            <para>
In this sample stats module, we want to provide three attributes the user
can chose from: A list of queues, a list of states an a time drop down. To
get the values shown in the drop down, some operations are needed. In this
case call StateList and GetAllQueues.
            </para>
            <para>
Then the list of attributes is created. Each attribute is defined via a
hashreference. You can use these keys:
            </para>
            <para>
                <itemizedlist mark="round" >
                    <listitem>
                        <para>Name</para>
                        <para>the label in the web interface</para>
                    </listitem>
                    <listitem>
                        <para>UseAsXvalue</para>
                        <para>Can this attribute be used on the x-axis</para>
                    </listitem>
                    <listitem>
                        <para>UseAsValueSeries</para>
                        <para>Can this attribute be used on the y-axis</para>
                    </listitem>
                    <listitem>
                        <para>UseAsRestriction</para>
                        <para>Can this attribute be used for restrictions.</para>
                    </listitem>
                    <listitem>
                        <para>Element</para>
                        <para>the HTML fieldname</para>
                    </listitem>
                    <listitem>
                        <para>Block</para>
                        <para>the block name in the template file
(e.g. &lt;OTRS_HOME&gt;/Kernel/Output/HTML/Standard/AgentStatsEditXaxis.dtl)</para>
                    </listitem>
                    <listitem>
                        <para>Values</para>
                        <para>the values shown in the attribute</para>
                    </listitem>
                </itemizedlist>
            </para>
            <para>
Hint: If you install this sample an you configured a statistic with some
queues - lets say 'queue A' and 'queue B' - then these queues are the only
ones that are shown to the user when he starts the statistic. Sometimes a
dynamic drop down or multiselect field is needed. In this case, you can set
"SelectedValues" in the definition of the attribute:
            </para>
            <para>
                <programlisting><![CDATA[
        {
            Name             => 'Created in Queue',
            UseAsXvalue      => 1,
            UseAsValueSeries => 1,
            UseAsRestriction => 1,
            Element          => 'CreatedQueueIDs',
            Block            => 'MultiSelectField',
            Translation      => 0,
            Values           => \%QueueList,
            SelectedValues   => [ @SelectedQueues ],
        },
                ]]></programlisting>
            </para>
            <para>
                <programlisting><![CDATA[
sub GetStatElement {
    my ( $Self, %Param ) = @_;

    # search tickets
    return $Self->{TicketObject}->TicketSearch(
        UserID     => 1,
        Result     => 'COUNT',
        Permission => 'ro',
        Limit      => 100_000_000,
        %Param,
    );
}
                ]]></programlisting>
            </para>
            <para>
GetStatElement gets called for each cell in the result table. So it should
be a numeric value. In this sample it does a simple ticket search. The hash
%Param contains information about the "current" x-value and the y-value as
well as any restrictions. So, for a cell that should count the created
tickets for queue 'Misc' with state 'open' the passed parameter hash looks
something like this:
            </para>
            <para>
                <programlisting><![CDATA[
    'CreatedQueueIDs' => [
        '4'
    ],
    'StateIDs' => [
        '2'
    ]
                ]]></programlisting>
            </para>
            <para>
If the "per cell" calculation should be avoided, GetStatTable is an
alternative. GetStatTable returns a list of rows, hence an array of
arrayreferences. This leads to the same result as using GetStatElement
            </para>
            <para>
                <programlisting><![CDATA[
sub GetStatTable {
    my ( $Self, %Param ) = @_;

    my @StatData;

    for my $StateName ( keys %{ $Param{TableStructure} } ) {
        my @Row;
        for my $Params ( @{ $Param{TableStructure}->{$StateName} } ) {
            my $Tickets = $Self->{TicketObject}->TicketSearch(
                UserID     => 1,
                Result     => 'COUNT',
                Permission => 'ro',
                Limit      => 100_000_000,
                %{$Params},
            );

            push @Row, $Tickets;
        }

        push @StatData, [ $StateName, @Row ];
    }

    return @StatData;
}
                ]]></programlisting>
            </para>
            <para>
GetStatTable gets all information about the stats query that is needed. The
passed parameters contains information about the attributes (Restrictions,
attributes that are used for x/y-axis) and the table structure. The table
structure is a hash reference where the keys are the values of the y-axis
and their values are hashreferences with the parameters used for
GetStatElement subroutines.
            </para>
            <para>
                <programlisting><![CDATA[
    'Restrictions' => {},
    'TableStructure' => {
        'closed successful' => [
            {
                'CreatedQueueIDs' => [
                    '3'
                ],
                'StateIDs' => [
                    '2'
                ]
            },
        ],
        'closed unsuccessful' => [
            {
                'CreatedQueueIDs' => [
                    '3'
                ],
                'StateIDs' => [
                    '3'
                ]
            },
        ],
    },
    'ValueSeries' => [
        {
            'Block' => 'MultiSelectField',
            'Element' => 'StateIDs',
            'Name' => 'State',
            'SelectedValues' => [
                '5',
                '3',
                '2',
                '1',
                '4'
            ],
            'Translation' => 1,
            'Values' => {
                '1' => 'new',
                '10' => 'closed with workaround',
                '2' => 'closed successful',
                '3' => 'closed unsuccessful',
                '4' => 'open',
                '5' => 'removed',
                '6' => 'pending reminder',
                '7' => 'pending auto close+',
                '8' => 'pending auto close-',
                '9' => 'merged'
            }
        }
    ],
    'XValue' => {
        'Block' => 'MultiSelectField',
        'Element' => 'CreatedQueueIDs',
        'Name' => 'Created in Queue',
        'SelectedValues' => [
            '3',
            '4',
            '1',
            '2'
        ],
        'Translation' => 0,
        'Values' => {
            '1' => 'Postmaster',
            '2' => 'Raw',
            '3' => 'Junk',
            '4' => 'Misc'
        }
    }
                ]]></programlisting>
            </para>
            <para>
Sometimes the headers of the table have to be changed. In that case, a
subroutine called GetHeaderLine has to be implemented. That subroutine has
to return an arrayreference with the column headers as elements. It gets
information about the x-values passed.
            </para>
            <para>
                <programlisting><![CDATA[
sub GetHeaderLine {
    my ( $Self, %Param ) = @_;

    my @HeaderLine = ('');
    for my $SelectedXValue ( @{ $Param{XValue}->{SelectedValues} } ) {
        push @HeaderLine, $Param{XValue}->{Values}->{$SelectedXValue};
    }

    return \@HeaderLine;
}
                ]]></programlisting>
            </para>
            <para>
                <programlisting><![CDATA[
sub ExportWrapper {
    my ( $Self, %Param ) = @_;

    # wrap ids to used spelling
    for my $Use (qw(UseAsValueSeries UseAsRestriction UseAsXvalue)) {
        ELEMENT:
        for my $Element ( @{ $Param{$Use} } ) {
            next ELEMENT if !$Element || !$Element->{SelectedValues};
            my $ElementName = $Element->{Element};
            my $Values      = $Element->{SelectedValues};

            if ( $ElementName eq 'QueueIDs' || $ElementName eq 'CreatedQueueIDs' ) {
                ID:
                for my $ID ( @{$Values} ) {
                    next ID if !$ID;
                    $ID->{Content} = $Self->{QueueObject}->QueueLookup( QueueID => $ID->{Content} );
                }
            }
            elsif ( $ElementName eq 'StateIDs' || $ElementName eq 'CreatedStateIDs' ) {
                my %StateList = $Self->{StateObject}->StateList( UserID => 1 );
                ID:
                for my $ID ( @{$Values} ) {
                    next ID if !$ID;
                    $ID->{Content} = $StateList{ $ID->{Content} };
                }
            }
        }
    }
    return \%Param;
}
                ]]></programlisting>
            </para>
            <para>
Configured statistics can be exported into XML format. But as queues with
the same queue names can have different IDs on different OTRS instances it
would be quite painful to export the IDs (the statistics would calculate the
wrong numbers then). So an export wrapper should be written to use the names
instead of ids. This should be done for each "dimension" of the stats module
(x-axis, y-axis and restrictions).
            </para>
            <para>
ImportWrapper works the other way around - it converts the name to the ID in
the instance the configuration is imported to.
            </para>
            <para>
This is a sample export:
            </para>
            <para>
                <programlisting><![CDATA[
<?xml version="1.0" encoding="utf-8"?>

<otrs_stats>
<Cache>0</Cache>
<Description>Sample stats module</Description>
<File></File>
<Format>CSV</Format>
<Format>Print</Format>
<Object>DeveloperManualSample</Object>
<ObjectModule>Kernel::System::Stats::Dynamic::DynamicStatsTemplate</ObjectModule>
<ObjectName>Sample Statistics</ObjectName>
<Permission>stats</Permission>
<StatType>dynamic</StatType>
<SumCol>0</SumCol>
<SumRow>0</SumRow>
<Title>Sample 1</Title>
<UseAsValueSeries Element="StateIDs" Fixed="1">
<SelectedValues>removed</SelectedValues>
<SelectedValues>closed unsuccessful</SelectedValues>
<SelectedValues>closed successful</SelectedValues>
<SelectedValues>new</SelectedValues>
<SelectedValues>open</SelectedValues>
</UseAsValueSeries>
<UseAsXvalue Element="CreatedQueueIDs" Fixed="1">
<SelectedValues>Junk</SelectedValues>
<SelectedValues>Misc</SelectedValues>
<SelectedValues>Postmaster</SelectedValues>
<SelectedValues>Raw</SelectedValues>
</UseAsXvalue>
<Valid>1</Valid>
</otrs_stats>
                ]]></programlisting>
            </para>
            <para>
Now, that all subroutines are explained, this is the complete sample stats
module.
            </para>
            <para>
                <programlisting><![CDATA[
# --
# Kernel/System/Stats/Dynamic/DynamicStatsTemplate.pm - all advice functions
# Copyright (C) 2001-2010 OTRS AG, http://otrs.org/
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (AGPL). If you
# did not receive this file, see http://www.gnu.org/licenses/agpl.txt.
# --

package Kernel::System::Stats::Dynamic::DynamicStatsTemplate;

use strict;
use warnings;

use Kernel::System::Queue;
use Kernel::System::State;
use Kernel::System::Ticket;

use vars qw($VERSION);
$VERSION = qw($Revision: 1.2 $) [1];

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    # check needed objects
    for my $Object (
        qw(DBObject ConfigObject LogObject UserObject TimeObject MainObject EncodeObject)
        )
    {
        $Self->{$Object} = $Param{$Object} || die "Got no $Object!";
    }

    # created needed objects
    $Self->{QueueObject}    = Kernel::System::Queue->new( %{$Self} );
    $Self->{TicketObject}   = Kernel::System::Ticket->new( %{$Self} );
    $Self->{StateObject}    = Kernel::System::State->new( %{$Self} );

    return $Self;
}

sub GetObjectName {
    my ( $Self, %Param ) = @_;

    return 'Sample Statistics';
}

sub GetObjectAttributes {
    my ( $Self, %Param ) = @_;

    # get state list
    my %StateList = $Self->{StateObject}->StateList(
        UserID => 1,
    );

    # get queue list
    my %QueueList = $Self->{QueueObject}->GetAllQueues();

    # get current time to fix bug#3830
    my $TimeStamp = $Self->{TimeObject}->CurrentTimestamp();
    my ($Date) = split /\s+/, $TimeStamp;
    my $Today = sprintf "%s 23:59:59", $Date;

    my @ObjectAttributes = (
        {
            Name             => 'State',
            UseAsXvalue      => 1,
            UseAsValueSeries => 1,
            UseAsRestriction => 1,
            Element          => 'StateIDs',
            Block            => 'MultiSelectField',
            Values           => \%StateList,
        },
        {
            Name             => 'Created in Queue',
            UseAsXvalue      => 1,
            UseAsValueSeries => 1,
            UseAsRestriction => 1,
            Element          => 'CreatedQueueIDs',
            Block            => 'MultiSelectField',
            Translation      => 0,
            Values           => \%QueueList,
        },
        {
            Name             => 'Create Time',
            UseAsXvalue      => 1,
            UseAsValueSeries => 1,
            UseAsRestriction => 1,
            Element          => 'CreateTime',
            TimePeriodFormat => 'DateInputFormat',    # 'DateInputFormatLong',
            Block            => 'Time',
            TimeStop         => $Today,
            Values           => {
                TimeStart => 'TicketCreateTimeNewerDate',
                TimeStop  => 'TicketCreateTimeOlderDate',
            },
        },
    );

    return @ObjectAttributes;
}

sub GetStatElement {
    my ( $Self, %Param ) = @_;

    # search tickets
    return $Self->{TicketObject}->TicketSearch(
        UserID     => 1,
        Result     => 'COUNT',
        Permission => 'ro',
        Limit      => 100_000_000,
        %Param,
    );
}

sub ExportWrapper {
    my ( $Self, %Param ) = @_;

    # wrap ids to used spelling
    for my $Use (qw(UseAsValueSeries UseAsRestriction UseAsXvalue)) {
        ELEMENT:
        for my $Element ( @{ $Param{$Use} } ) {
            next ELEMENT if !$Element || !$Element->{SelectedValues};
            my $ElementName = $Element->{Element};
            my $Values      = $Element->{SelectedValues};

            if ( $ElementName eq 'QueueIDs' || $ElementName eq 'CreatedQueueIDs' ) {
                ID:
                for my $ID ( @{$Values} ) {
                    next ID if !$ID;
                    $ID->{Content} = $Self->{QueueObject}->QueueLookup( QueueID => $ID->{Content} );
                }
            }
            elsif ( $ElementName eq 'StateIDs' || $ElementName eq 'CreatedStateIDs' ) {
                my %StateList = $Self->{StateObject}->StateList( UserID => 1 );
                ID:
                for my $ID ( @{$Values} ) {
                    next ID if !$ID;
                    $ID->{Content} = $StateList{ $ID->{Content} };
                }
            }
        }
    }
    return \%Param;
}

sub ImportWrapper {
    my ( $Self, %Param ) = @_;

    # wrap used spelling to ids
    for my $Use (qw(UseAsValueSeries UseAsRestriction UseAsXvalue)) {
        ELEMENT:
        for my $Element ( @{ $Param{$Use} } ) {
            next ELEMENT if !$Element || !$Element->{SelectedValues};
            my $ElementName = $Element->{Element};
            my $Values      = $Element->{SelectedValues};

            if ( $ElementName eq 'QueueIDs' || $ElementName eq 'CreatedQueueIDs' ) {
                ID:
                for my $ID ( @{$Values} ) {
                    next ID if !$ID;
                    if ( $Self->{QueueObject}->QueueLookup( Queue => $ID->{Content} ) ) {
                        $ID->{Content}
                            = $Self->{QueueObject}->QueueLookup( Queue => $ID->{Content} );
                    }
                    else {
                        $Self->{LogObject}->Log(
                            Priority => 'error',
                            Message  => "Import: Can' find the queue $ID->{Content}!"
                        );
                        $ID = undef;
                    }
                }
            }
            elsif ( $ElementName eq 'StateIDs' || $ElementName eq 'CreatedStateIDs' ) {
                ID:
                for my $ID ( @{$Values} ) {
                    next ID if !$ID;

                    my %State = $Self->{StateObject}->StateGet(
                        Name  => $ID->{Content},
                        Cache => 1,
                    );
                    if ( $State{ID} ) {
                        $ID->{Content} = $State{ID};
                    }
                    else {
                        $Self->{LogObject}->Log(
                            Priority => 'error',
                            Message  => "Import: Can' find state $ID->{Content}!"
                        );
                        $ID = undef;
                    }
                }
            }
        }
    }
    return \%Param;
}

1;
                ]]></programlisting>
            </para>
        </section>
        <section id="module-stats-dynamic-configuration">
            <title>Configuration example</title>
            <para>
                <programlisting><![CDATA[
<?xml version="1.0" encoding="iso-8859-1" ?>
<otrs_config version="1.0" init="Config">
    <ConfigItem Name="Stats::DynamicObjectRegistration###DynamicStatsTemplate" Required="0" Valid="1">
        <Description Lang="en">Here you can decide if the common stats module may generate stats about the number of default tickets a requester created.</Description>
        <Group>Framework</Group>
        <SubGroup>Core::Stats</SubGroup>
        <Setting>
            <Hash>
                <Item Key="Module">Kernel::System::Stats::Dynamic::DynamicStatsTemplate</Item>
            </Hash>
        </Setting>
    </ConfigItem>
</otrs_config>
                ]]></programlisting>
            </para>
        </section>
        <section id="module-stats-dynamic-use_cases">
            <title>Use case examples</title>
            <para>
Use cases.
            </para>
        </section>
        <section id="module-stats-dynamic-caveats">
            <title>Caveats and Warnings</title>
            <para>
If you have a lot of cells in the result table and the GetStatElement is
quite complex, the request can take a long time.
            </para>
        </section>
        <section id="module-stats-dynamic-releases">
            <title>Release Availability</title>
            <para>
Dynamic stat modules are available since OTRS 2.0.
            </para>
        </section>
    </section>
    <section id="module-stats-static">
        <title>Static Stats</title>
        <para>
The subsequent paragraphs describe the static stats. Static stats are very
easy to create as these modules have to implement only three subroutines.
        </para>
        <para>
            <itemizedlist mark="round">
                <listitem>
                    <para>neu</para>
                </listitem>
                <listitem>
                    <para>Param</para>
                </listitem>
                <listitem>
                    <para>Run</para>
                </listitem>
            </itemizedlist>
        </para>
        <section id="module-stats-static-subroutines">
            <title>Code example</title>
            <para>
The following paragraphs describe the subroutines needed in a static stats.
            </para>
            <para>
                <programlisting><![CDATA[
sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {%Param};
    bless( $Self, $Type );

    # check all needed objects
    for my $Needed (
        qw(DBObject ConfigObject LogObject
        TimeObject MainObject EncodeObject)
        )
    {
        $Self->{$Needed} = $Param{$Needed} || die "Got no $Needed";
    }

    # create needed objects
    $Self->{TypeObject}   = Kernel::System::Type->new( %{$Self} );
    $Self->{TicketObject} = Kernel::System::Ticket->new( %{$Self} );
    $Self->{QueueObject}  = Kernel::System::Queue->new( %{$Self} );

    return $Self;
}
                ]]></programlisting>
            </para>
            <para>
new creates a new instance of the static stats class. First it creates a new
object and then it checks for the needed objects.
            </para>
            <para>
                <programlisting><![CDATA[
sub Param {
    my $Self = shift;

    my %Queues = $Self->{QueueObject}->GetAllQueues();
    my %Types  = $Self->{TypeObject}->TypeList(
        Valid => 1,
    );

    my @Params = (
        {
            Frontend  => 'Type',
            Name      => 'TypeIDs',
            Multiple  => 1,
            Size      => 3,
            Data      => \%Types,
        },
        {
            Frontend  => 'Queue',
            Name      => 'QueueIDs',
            Multiple  => 1,
            Size      => 3,
            Data      => \%Queues,
        },
    );

    return @Params;
}
                ]]></programlisting>
            </para>
            <para>
The Param method provides the list of all parameters/attributes that can be
selected to create a static stat. It gets some parameters passed: The values
for the stats attributes provided in a request, the format of the stats and
the name of the object (name of the module).
            </para>
            <para>
The parameters/attributes have to be hashreferences with these
key-value-pairs.
            </para>
            <para>
                <itemizedlist mark="round" >
                    <listitem>
                        <para>Frontent</para>
                        <para>the label in the web interface</para>
                    </listitem>
                    <listitem>
                        <para>Name</para>
                        <para>the HTML fieldname</para>
                    </listitem>
                    <listitem>
                        <para>Data</para>
                        <para>the values shown in the attribute</para>
                    </listitem>
                </itemizedlist>
            </para>
            <para>
Other parameter for the BuildSelection method of the LayoutObject can be
used, as it is done with "Size" and "Multiple" in this sample module.
            </para>
            <para>
                <programlisting><![CDATA[
sub Run {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(TypeIDs QueueIDs)) {
        if ( !$Param{$Needed} ) {
            $Self->{LogObject}->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # set report title
    my $Title = 'Tickets per Queue';

    # table headlines
    my @HeadData = (
        'Ticket Number',
        'Queue',
        'Type',
    );

    my @Data;
    my @TicketIDs = $Self->{TicketObject}->TicketSearch(
        UserID     => 1,
        Result     => 'ARRAY',
        Permission => 'ro',
        %Param,
    );

    for my $TicketID ( @TicketIDs ) {
        my %Ticket = $Self->{TicketObject}->TicketGet(
            UserID => 1,
            TicketID => $TicketID,
        );
        push @Data, [ $Ticket{TicketNumber}, $Ticket{Queue}, $Ticket{Type} ];
    }

    return ( [$Title], [@HeadData], @Data );
}
                ]]></programlisting>
            </para>
            <para>
The Run method actually generates the table data for the stats. It gets the
attributes for this stats passed. In this sample it in %Param a key
'TypeIDs' and a key 'QueueIDs' exist (see attributes in Param method) and
their values are arrayreferences. The returned data consists of three parts:
Two arrayreferences and an array. In the first arrayreference the title for
the statistic is stored, the second arrayreference contains the headlines
for the columns in the table. And then the data for the table body follow.
            </para>
            <para>
                <programlisting><![CDATA[
# --
# Kernel/System/Stats/Static/StaticStatsTemplate.pm
# Copyright (C) 2001-2010 OTRS AG, http://otrs.org/
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (AGPL). If you
# did not receive this file, see http://www.gnu.org/licenses/agpl.txt.
# --

package Kernel::System::Stats::Static::StaticStatsTemplate;

use strict;
use warnings;

use Kernel::System::Type;
use Kernel::System::Ticket;
use Kernel::System::Queue;

use vars qw($VERSION);
$VERSION = qw($Revision: 1.2 $) [1];

=head1 NAME

StaticStatsTemplate.pm - the module that creates the stats about tickets in a queue

=head1 SYNOPSIS

All functions

=head1 PUBLIC INTERFACE

=over 4

=cut

=item new()

create an object

    use Kernel::Config;
    use Kernel::System::Encode;
    use Kernel::System::Log;
    use Kernel::System::Main;
    use Kernel::System::Time;
    use Kernel::System::DB;
    use Kernel::System::Stats::Static::StaticStatsTemplate;

    my $ConfigObject = Kernel::Config->new();
    my $EncodeObject = Kernel::System::Encode->new(
        ConfigObject => $ConfigObject,
    );
    my $LogObject    = Kernel::System::Log->new(
        ConfigObject => $ConfigObject,
    );
    my $MainObject = Kernel::System::Main->new(
        ConfigObject => $ConfigObject,
        LogObject    => $LogObject,
    );
    my $TimeObject = Kernel::System::Time->new(
        ConfigObject => $ConfigObject,
        LogObject    => $LogObject,
    );
    my $DBObject = Kernel::System::DB->new(
        ConfigObject => $ConfigObject,
        LogObject    => $LogObject,
        MainObject   => $MainObject,
    );
    my $StatsObject = Kernel::System::Stats::Static::StaticStatsTemplate->new(
        ConfigObject => $ConfigObject,
        LogObject    => $LogObject,
        MainObject   => $MainObject,
        TimeObject   => $TimeObject,
        DBObject     => $DBObject,
        EncodeObject => $EncodeObject,
    );

=cut

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {%Param};
    bless( $Self, $Type );

    # check all needed objects
    for my $Needed (
        qw(DBObject ConfigObject LogObject
        TimeObject MainObject EncodeObject)
        )
    {
        $Self->{$Needed} = $Param{$Needed} || die "Got no $Needed";
    }

    # create needed objects
    $Self->{TypeObject}   = Kernel::System::Type->new( %{$Self} );
    $Self->{TicketObject} = Kernel::System::Ticket->new( %{$Self} );
    $Self->{QueueObject}  = Kernel::System::Queue->new( %{$Self} );

    return $Self;
}

=item Param()

Get all parameters a user can specify.

    my @Params = $StatsObject->Param();

=cut

sub Param {
    my $Self = shift;

    my %Queues = $Self->{QueueObject}->GetAllQueues();
    my %Types  = $Self->{TypeObject}->TypeList(
        Valid => 1,
    );

    my @Params = (
        {
            Frontend  => 'Type',
            Name      => 'TypeIDs',
            Multiple  => 1,
            Size      => 3,
            Data      => \%Types,
        },
        {
            Frontend  => 'Queue',
            Name      => 'QueueIDs',
            Multiple  => 1,
            Size      => 3,
            Data      => \%Queues,
        },
    );

    return @Params;
}

=item Run()

generate the statistic.

    my $StatsInfo = $StatsObject->Run(
        TypeIDs  => [
            1, 2, 4
        ],
        QueueIDs => [
            3, 4, 6
        ],
    );

=cut

sub Run {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(TypeIDs QueueIDs)) {
        if ( !$Param{$Needed} ) {
            $Self->{LogObject}->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # set report title
    my $Title = 'Tickets per Queue';

    # table headlines
    my @HeadData = (
        'Ticket Number',
        'Queue',
        'Type',
    );

    my @Data;
    my @TicketIDs = $Self->{TicketObject}->TicketSearch(
        UserID     => 1,
        Result     => 'ARRAY',
        Permission => 'ro',
        %Param,
    );

    for my $TicketID ( @TicketIDs ) {
        my %Ticket = $Self->{TicketObject}->TicketGet(
            UserID => 1,
            TicketID => $TicketID,
        );
        push @Data, [ $Ticket{TicketNumber}, $Ticket{Queue}, $Ticket{Type} ];
    }

    return ( [$Title], [@HeadData], @Data );
}

1;

=back

=head1 TERMS AND CONDITIONS

This software is part of the OTRS project (http://otrs.org/).

This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see http://www.gnu.org/licenses/agpl.txt.

=head1 VERSION

$Revision: 1.2 $ $Date: 2010-12-01 13:31:52 $

=cut
                ]]></programlisting>
            </para>
        </section>
        <section id="module-stats-static-configuration">
            <title>Configuration example</title>
            <para>
There is no configuration needed. Right after installation, the module is
available to create a statistic for this module.
            </para>
        </section>
        <section id="module-stats-static-use_cases">
            <title>Use case examples</title>
            <para>
Use cases.
            </para>
        </section>
        <section id="module-stats-static-caveats">
            <title>Caveats and Warnings</title>
            <para>
Caveats and Warnings for static stats.
            </para>
        </section>
        <section id="module-stats-static-releases">
            <title>Release Availability</title>
            <para>
Static stat modules are available since OTRS 1.3.
            </para>
        </section>
        <section id="module-stats-static-using-old-stats" >
            <title>Using old static stats</title>
            <para>
Standard OTRS versions 1.3 and 2.0 already facilitated the generation of
stats.  Various stats for OTRS versions 1.3 and 2.0 which have been
specially developed to meet customers' requirements can be used in more
recent versions too.
            </para>
            <para>
The files must merely be moved from the
<filename>Kernel/System/Stats/</filename> path to
<filename>Kernel/System/Stats/Static/</filename>. Additionally the package
name of the respective script must be amended by "::Static".
            </para>
            <para>
The following example shows how the first path is amended.
            </para>
            <para>
                <programlisting><![CDATA[
    package Kernel::System::Stats::AccountedTime;
                ]]></programlisting>
            </para>
            <para>
                <programlisting><![CDATA[
    package Kernel::System::Stats::Static::AccountedTime;
                ]]></programlisting>
            </para>
        </section>
    </section>
</section>
